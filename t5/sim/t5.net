*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

Vcc 7 0 10.0
Vee 4 0 -10.0
Vin 1 0 0 ac 1.0 sin(0 10m 1k)

X1 3 2 7 4 6 uA741

R1 3 0 1k
R2 6 out 1k
R3 6 2 150k
R4 2 0 967.74

C1 1 3  220nF
C2 out 0 110nF


.op
.end

.control

*makes plots in color
set hcopypscolor=1
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0

print all


* time analysis
tran 1e-5 1e-2 1e-3 
plot v(out)
*plot v(1)
hardcopy vout.eps v(out)


* frequency analysis
ac dec 10 10 100MEG
*plot vdb(3)
*plot vdb(6)
plot vdb (out) 40
*print vdb(out)
*plot vdb(1)
*plot v(out)/v(1)
plot vp(out)
hardcopy vbdout.eps vdb(out) 40
hardcopy vpout.eps vp(out)

*let gain = vecmax(vdb(out))
*let lingain = vecmax(v(out))

let  cutoff = vecmax(vdb(out))-3
meas ac flow WHEN vdb(out)=cutoff RISE=1
meas ac fhigh WHEN vdb(out)=cutoff CROSS=LAST
*let bandwidth = f2-flow
*print vdb(out)
*print gain
let fc = sqrt(flow*fhigh)
*print fc
*print flow
*print fhigh

meas ac gain FIND vdb(out) AT=fc
meas ac lingain  FIND v(out) AT=fc

*print lingain

let cost = 13223.09204 + 333 + 0.66
print cost

let merit = 1.0/(cost*(abs(1000-fc)*abs(lingain-100))+ 0.000001)
*print merit

*input impedance in kohm

meas ac curr_in FIND vin#branch AT=fc
meas ac volt_in FIND v(1) AT=fc

print volt_in
print curr_in
let zin =  -volt_in/curr_in

echo "sim_TAB"
print gain
print flow
print fhigh
print fc
print merit
print zin
echo "sim_END"
echo "let fc = " "$&fc" > fc.txt



*need a different setup to measure output impedance

.endc 

